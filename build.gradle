plugins {
  id 'org.asciidoctor.convert' version '1.5.3'
  id 'com.github.jruby-gradle.base' version '1.2.1'
  id 'com.moowork.node' version '0.12'
  id 'com.moowork.gulp' version '0.12'
}

dependencies {
  gems('rubygems:asciidoctor-bespoke:1.0.0.alpha.1') {
    transitive false
  }
}

// TIP The pattern npm_<command_name> executes an NPM command.
node {
  version = '0.12.9'
  download = true // true tells plugin to manage node installation, false tells plugin to use system node
  workDir = file('gradle/nodejs')
  // NOTE set npmVersion if you want to upgrade npm
  //npmVersion = '2.15.3'
}

jrubyPrepare {
  outputDir "rubygems/jruby/${project.jruby.execVersion}"
}

task setup {
  dependsOn npmInstall, jrubyPrepare
}

// TIP The pattern gulp_<task_name> executes a gulp task; run gulpTasks to get list of tasks
gulp {
  bufferOutput = false
}

task gulpTasks(type: GulpTask) {
  args = ['--tasks-simple']
}

gulp_build.dependsOn = []
gulp_deploy.dependsOn = [build]

asciidoctorj {
  version = '1.5.4'
}

asciidoctor {
  dependsOn gulp_build
  gemPath = jrubyPrepare.outputDir
  requires 'asciidoctor-bespoke'
  backends 'bespoke'
  sourceDir 'src'
  sources {
    include 'index.adoc'
  }
  resources {
    from(sourceDir) {
      include 'images/**'
      include 'fonts/**'
    }
  }
  outputDir buildDir
}

build.dependsOn = [asciidoctor]

task deploy {
  dependsOn clean, gulp_deploy
}

apply plugin: 'jetty'

tasks.remove(tasks.jettyRunWar)
tasks.remove(tasks.jettyStop)

jettyRun {
  dependsOn = []
  contextPath ''
  httpPort 8000
  webAppSourceDirectory file("${project.buildDir}/bespoke")
}

task serve {
  dependsOn build, jettyRun
}
